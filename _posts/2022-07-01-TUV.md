---
title: 紫外線數據之處理 
tags: CAMx TOMS
layout: article
aside:
  toc: true
sidebar:
  nav: layouts
---

## 背景

## tuv.inp 內容


變數|內容|範例
-|-|-
program version|程式版本    |TUV4.8CAMx7.00
output file name|輸出檔名稱|./$OUTFILE
o3map file name|o3map檔名|../../toms/o3map.20$1.d$DOM
number of vert levels|垂直層數   | 11
levels, km agl|各層離地高Km| 0. 0.15 0.35 0.60 1.00 2.00 3.00 4.00 6.00 8.00 10.0
date (YYMMDD)|日期| $yymmdd
rad transfer scheme|自由基傳輸方式|$SCHEME
num of phot reactions|光化反應數| $RXNUM
TUV reaction #s|光解反應式    | $RXLST

## 腳本
### 選項

環境變數|內容|選項
-|-|-
MECH|化學機制|CB05, CB6, SAPRC07
SCHEME|計算方法|1 = pseudo spherical 2-stream/2 = discrete ordinates(recommended)
number of vert levels|垂直層數|11層或[15層][15]

### 腳本內容

```bash
#!/bin/csh -f
set MECH    =  CB6                  # [CB05|CB6|SAPRC07]
set SCHEME  =  2                    # 1 = pseudo spherical 2-stream
                                    # 2 = discrete ordinates (recommended)
set yymmdd  = $1                    # YYMMDD format
set DOM     = $2                    # 1,2,3,4format
set RUN_TUVCOMPR = 'NO'             # Run TUV comparison

if ($SCHEME == 1) set SCM = 'ps2str'
if ($SCHEME == 2) set SCM = 'do'
set TUVINP  = tuv.${SCM}_${MECH}.inp
set TUVOUT  = tuv.${SCM}_${MECH}.diag
set OUTFILE = tuv.${SCM}_${MECH}.$yymmdd.d$DOM

if ($MECH == 'CB6') then
  set RXNUM = 23
  set RXLST = '94, 95, 96, 97, 98, 99, 100, 101, 102, 103, 104, 105, 106, 107, 108, 109, 110, 114, 115, 116, 111, 112, 113'
endif
if ($MECH == 'CB05') then
  set RXNUM = 19
  set RXLST = '77, 84, 83, 79, 78, 87, 66, 88, 89, 90, 91, 63, 68, 67, 62, 92, 61, 93, 82'
endif
if ($MECH == 'SAPRC07') then
  set RXNUM = 29
  set RXLST = '168 169 170 172 173 161 158 159 155 175 157 156 149 148 143 165 152 154 153 166 146 147 145 164 167 163 160 144 174'
endif

cat << EOF >! ./tuv.inp
program version    |TUV4.8CAMx7.00
output file name   |./$OUTFILE
o3map file name    |../../toms/o3map.20$1.d$DOM
# of vert levels   | 11
levels, km agl     | 0. 0.15 0.35 0.60 1.00 2.00 3.00 4.00 6.00 8.00 10.0
date (YYMMDD)      | $yymmdd
rad transfer scheme| $SCHEME
# of phot reactions| $RXNUM
TUV reaction #s    | $RXLST
EOF


/cluster/src/CAMx/tuv/tuv4.8.camx7.00/src/tuv
mv -f tuv.inp $TUVINP
mv -f tuv_diag.out $TUVOUT

# Do a quick comparison of CAMx photolysis rate files
# and report the ratios in a flat ASCII format
# Note: Number of reactions in the two files have to be the same.

if ( $RUN_TUVCOMPR == 'YES' ) then
/cluster/src/CAMx/tuv/tuv4.8.camx7.00/tuvcompr/tuvcompr << EOF
rates file 1       |./tuv.do_${MECH}.$yymmdd.d$DOM
rates file 2       |./tuv.ps2str_${MECH}.$yymmdd
summary output file|./compare.${MECH}_do_ps2str
number of reactions| $RXNUM
number of altitudes| 15
EOF
endif
```

[15]: <> "num levels, km agl     | .0, .083, .168, .345,.610,.790,.972,1.346,1.676,2.339,3.619,5.525,7.444,12.583,20"